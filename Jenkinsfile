pipeline {
  agent any
  triggers { pollSCM('* * * * *') }
  environment {
    AWS_REGION     = 'us-east-1'
    REPO_NAME      = 'nm-fsm-app'
    PYTHONPATH     = "${WORKSPACE}"
    ANSIBLE_VAULT_PASSWORD_FILE = '/var/lib/jenkins/.ansible_vault_pass'  // Created by professor script and understood by Ansible
  }
  options {
    buildDiscarder(logRotator(numToKeepStr: '10'))
    timestamps(); ansiColor('xterm')
  }
  stages {
    // -- Stage 1: Checkout ----------------------------------------------
    stage('Checkout') {
      steps {
        checkout scm
        sh 'git log -1 --oneline'
        script {
          env.IMAGE_TAG = sh(script: 'git rev-parse --short HEAD',
                             returnStdout: true).trim()
          echo "Image tag for this build: ${env.IMAGE_TAG}"
        }
      }
    }
    // -- Stage 2: Unit & HTTP Tests-------------
    stage('Unit & HTTP Tests') {
      steps {
       sh 'pytest tests/unit/ tests/http/ -v'  
      }
      // pytest.ini auto-injects sqlite:///:memory:
    }
    // -- Stage 3: IaC Scan Checkov-------------
    stage('IaC Scan - Checkov') {
      steps {
        sh '''
          checkov -d $WORKSPACE/devops-course/terraform \
            --quiet \
            --compact \
            --framework terraform \
            --skip-check CKV_AWS_126,CKV_AWS_135,CKV_AWS_79,CKV_AWS_8,CKV_AWS_3,CKV_AWS_189,CKV2_AWS_2,CKV_AWS_163,CKV_AWS_51,CKV_AWS_136,CKV_AWS_333,CKV_AWS_158,CKV_AWS_338,CKV_AWS_23,CKV_AWS_382,CKV_AWS_65,CKV_AWS_272,CKV_AWS_116,CKV_AWS_115,CKV_AWS_117,CKV_AWS_50 \
            -o json > checkov-report.json 
         '''
      }
      post {
        always {
          archiveArtifacts artifacts: 'checkov-report.json',
                           allowEmptyArchive: true
        }
      }
    }
    // -- Stage 4: Rebuild Golden AMI only when image contents change-------------
    stage('Packer Build') {
      when {
        anyOf {
          changeset "devops-course/roles/**"
          changeset "devops-course/group_vars/**"
          changeset "devops-course/site.yml"
          changeset "devops-course/vault.yml"
          changeset "devops-course/mysql-ami.pkr.hcl"
        }
      }
      steps {
        sh '''
          set -e
          cd "$WORKSPACE/devops-course"
          packer init .
          packer validate .
          packer build mysql-ami.pkr.hcl
          # Preserve the build record outside the workspace — cleanWs() wipes it
          cp -f ami_manifest.json /home/ec2-user/NM-FSM-App/devops-course/ami_manifest.json
        '''
        archiveArtifacts artifacts: 'devops-course/ami_manifest.json', allowEmptyArchive: true
      }
    }
 // -- Stage 5: Build and push Docker image ------------------------------
  stage('Docker Build & Push') {
      steps {
        sh '''
          set -e
          : "${IMAGE_TAG:?IMAGE_TAG not set - run a full pipeline build}"
          TF=/home/ec2-user/NM-FSM-App/devops-course/terraform
          REPO_URL=$(TF_WORKSPACE=staging terraform -chdir=$TF output -raw ecr_repository_url)
          REGISTRY=${REPO_URL%/*}
          aws ecr get-login-password --region ${AWS_REGION} | \
            docker login --username AWS --password-stdin ${REGISTRY}
          docker build -t ${REPO_NAME}:${IMAGE_TAG} .
          # Immutable tag — this is what the pipeline deploys
          docker tag  ${REPO_NAME}:${IMAGE_TAG} ${REPO_URL}:${IMAGE_TAG}
          docker push ${REPO_URL}:${IMAGE_TAG}
          # Moving tag — keeps :latest current for a manual terraform apply
          docker tag  ${REPO_NAME}:${IMAGE_TAG} ${REPO_URL}:latest
          docker push ${REPO_URL}:latest
        '''
      }
    }
    stage('Terraform Test') {
      steps {
        sh '''
          set -e
          SRC=$WORKSPACE/devops-course/terraform
          DST=/home/ec2-user/NM-FSM-App/devops-course/terraform
          # First Terraform stage: sync the checked-out code into the state
          # directory. Stages 7 and 9 then operate on these same files.
          cp -f $SRC/*.tf $SRC/terraform.tfvars $DST/
          mkdir -p $DST/tests
          cp -f $SRC/tests/*.tftest.hcl $DST/tests/
          cd $DST
          terraform init -input=false
          TF_WORKSPACE=staging terraform test
        '''
      }
    }
   // -- Stage 7: Terraform Plan ---------------------------------------------
   stage('Terraform Plan') {
      steps {
        sh '''
          set -e
          SRC=$WORKSPACE/devops-course/terraform
          DST=/home/ec2-user/NM-FSM-App/devops-course/terraform  
          cp -f $SRC/*.tf $SRC/terraform.tfvars $DST/
          cd $DST
          terraform init
          terraform workspace select staging
          terraform plan -var="flask_image_tag=$IMAGE_TAG" -out=tfplan.bin
          terraform show -no-color tfplan.bin | tee tfplan.txt
          cp tfplan.txt $WORKSPACE/tfplan.txt
        '''
        archiveArtifacts artifacts: 'tfplan.txt'
      }
    }
   // -- Stage 8: Manual approval before infrastructure change ---------------
    stage('Approval') {
      steps {
        input message: 'Review Terraform plan. Proceed with apply?',
              ok: 'Apply', submitter: 'admin'
      }
    }
    // -- Stage 9: Terraform Apply --------------------------------------------
    stage('Terraform Apply') {
      steps {
        sh 'cd /home/ec2-user/NM-FSM-App/devops-course/terraform && terraform workspace select staging && terraform apply -auto-approve tfplan.bin'
      }
    }
    // -- Stage 10: Integration Tests --------------------------------------------
   stage('Integration Tests') {
     steps {
       withCredentials([string(credentialsId: 'db-password', variable: 'DB_PASS')]) {
         sh '''
           export DATABASE_URL="mysql+pymysql://flaskapp:${DB_PASS}@172.31.133.10/fsm_db"
           pytest tests/integration/ -v
         '''
       }
     }
   }
   // -- Stage 11: Perform E2E testing with Playwright ------------------------------
    stage('E2E Tests') {
     steps {
        sh '''
         aws ecs wait services-stable \
            --cluster cis4641-cluster --services flask-staging
          TASK_ARN=$(aws ecs list-tasks \
            --cluster cis4641-cluster \
            --service-name flask-staging \
            --desired-status RUNNING \
            --query "taskArns[0]" --output text)
         STAGING_IP=$(aws ecs describe-tasks \
            --cluster cis4641-cluster --tasks $TASK_ARN \
            --query 'tasks[0].attachments[0].details[?name==`privateIPv4Address`].value' \
            --output text)
          cd $WORKSPACE
          export STAGING_URL="http://${STAGING_IP}:5000"
          export PYTHONPATH=$WORKSPACE
          for i in $(seq 1 12); do
            curl -sf "$STAGING_URL/" > /dev/null && break
            echo "Waiting for app... ($i)"
            sleep 5
          done
          pytest tests/e2e/ -v --tb=short
        '''
      }
    }
   // -- Stage 12: Manual gate then promote to production workspace ------------------------------
   stage('Promote to Production') {
      steps {
        input message: 'Staging verified. Promote to production?',
              ok: 'Promote', submitter: 'admin'
        sh '''
          set -e
          cd /home/ec2-user/NM-FSM-App/devops-course/terraform
          terraform workspace select production
          terraform apply -auto-approve -var="flask_image_tag=$IMAGE_TAG"
        '''
      }
    }
  } // end stages


  post {
    success { echo 'Deployment to staging and production complete.' }
    failure { echo 'Pipeline FAILED. Check logs above.' }
    always  { cleanWs() }
  }
}
